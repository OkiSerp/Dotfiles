#!/usr/bin/env fish

argparse --ignore-unknown \
  "u/up" "d/down" "l/level=" "s/set=" "t/status" -- $argv

not type -q brightnessctl && exit

set -q _flag_set && brightnessctl --quiet set "$_flag_set%" && exit

if set -q _flag_level
  if set -q _flag_up
    brightnessctl --quiet set "+$_flag_level%"
  else if set -q _flag_down
    brightnessctl --quiet set "$_flag_level%-"
  end
  exit
end

function print_status
  set -l current (brightnessctl get) && set -l maximum (brightnessctl max)
  set -l brightness (math "100 * $current / $maximum")
  set -l icon "ó± ‚"

  set -l c_low "^c#d20f39^"
  set -l c_middle "^c#df8e1d^"
  set -l c_high "^c#209fb5^"
  set -l c_reset "^c#cad3f5^"

  [ $brightness -le 20 ] && \
    printf "%s%s %s%s%%" $c_low $icon $c_reset (math "round($brightness)")

  [ $brightness -gt 20 ] && [ $brightness -lt 50 ] && \
    printf "%s%s %s%s%%" $c_middle $icon $c_reset (math "round($brightness)")

  [ $brightness -ge 50 ] && \
    printf "%s%s %s%s%%" $c_high $icon $c_reset (math "round($brightness)")
end

if set -q _flag_status
  print_status
end
