#!/usr/bin/env fish

#  ________  ___       __   _____ ______
# |\   ___ \|\  \     |\  \|\   _ \  _   \
# \ \  \_|\ \ \  \    \ \  \ \  \\\__\ \  \
#  \ \  \ \\ \ \  \  __\ \  \ \  \\|__| \  \
#   \ \  \_\\ \ \  \|\__\_\  \ \  \    \ \  \
#    \ \_______\ \____________\ \__\    \ \__\
#     \|_______|\|____________|\|__|     \|__|
#
# DWM Installation Script
# by Oleksii Kapula

argparse -i "x/xorg" -- $argv

set -l deps \
  "base-devel" "git" \
  "libx11" "libxft" "libxinerama" \
  "feh" "brightnessctl" "sxhkd" "unclutter" \
  "pipewire" "pipewire-alsa" "pipewire-pulse" "pipewire-jack" \
  "wireplumber" "alsa-utils" "calc"

if set -q _flag_xorg
  set -a deps "xorg"
else
  set -l xdeps "server" "xinit" "xprop" \
    "xsetroot" "xset" "xinput" "setxkbmap"

  for dep in $xdeps
    set -a deps (string join "" "xorg-" $dep)
  end
end

if not type -q yay
  sudo pacman --sync --needed base-devel git
  git clone https://aur.archlinux.org/yay-bin.git
  cd yay-bin/ && makepkg -si
  cd - && rm yay-bin/ -rf
end

yay --sync --needed $deps

set -l sldir $HOME/.local/state/suckless

git clone https://github.com/okisrp/suckless.git $sldir

set -l tools "dwm" "dmenu"

for tool in $tools
  cd $sldir/$tool
  make && sudo make clean install
  cd -
end
