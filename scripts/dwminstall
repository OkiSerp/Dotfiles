#!/usr/bin/env fish

#  ________  ___       __   _____ ______
# |\   ___ \|\  \     |\  \|\   _ \  _   \
# \ \  \_|\ \ \  \    \ \  \ \  \\\__\ \  \
#  \ \  \ \\ \ \  \  __\ \  \ \  \\|__| \  \
#   \ \  \_\\ \ \  \|\__\_\  \ \  \    \ \  \
#    \ \_______\ \____________\ \__\    \ \__\
#     \|_______|\|____________|\|__|     \|__|
#
# DWM Installation Script
# by Oleksii Kapula

argparse -i "x/xorg" "m/make" "s/session" -- $argv

set -l deps \
  "base-devel" "git" \
  "libx11" "libxft" "libxinerama" \
  "feh" "brightnessctl" "sxhkd" "xbanish" \
  "pipewire" "pipewire-alsa" "pipewire-pulse" "pipewire-jack" \
  "wireplumber" "alsa-utils" "calc"

if set -q _flag_xorg
  set -a deps "xorg"
else
  set -l xdeps "server" "xinit" "xprop" \
    "xsetroot" "xset" "xinput" "setxkbmap"

  for dep in $xdeps
    set -a deps (string join "" "xorg-" $dep)
  end
end

if not type -q yay
  sudo pacman --sync --needed base-devel git
  git clone https://aur.archlinux.org/yay-bin.git
  cd yay-bin/ && makepkg -si
  cd - && rm yay-bin/ -rf
end

yay --sync --needed $deps

set -l sldir $HOME/.suckless

if not test -d $sldir
  mkdir -p $sldir
end

set -l tools "dwm" "dmenu" "st"

for tool in $tools
  if test -d $sldir/$tool; continue; end
  git clone https://git.suckless.org/$tool $sldir/$tool
  if set -q _flag_make
    cd $sldir/$tool
    make && sudo make clean install
    cd -
  end
end

if set -q _flag_session
  set -l dwmdesktop $HOME/dwm.desktop
  set -l sessiondir /usr/bin/xsessions

  printf "%s\n%s\n%s\n%s\n%s" \
    "[Desktop Entry]" \
    "Type=XSession" \
    "Exec=$(which dwm 2> /dev/null || echo 'dwm')" \
    "Comment=Dynamic Window Manager" \
    "Name=DWM" > $dwmdesktop

  if not test -d $sessiondir
    sudo mkdir $sessiondir
    sudo mv $dwmdesktop $sessiondir
  else if test -d $sessiondir && not test -e $sessiondir/$dwmdesktop
    sudo mv $dwmdesktop $sessiondir
  else
    rm $dwmdesktop
  end
end
