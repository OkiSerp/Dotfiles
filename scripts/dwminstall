#!/usr/bin/env fish

#  ________  ___       __   _____ ______
# |\   ___ \|\  \     |\  \|\   _ \  _   \
# \ \  \_|\ \ \  \    \ \  \ \  \\\__\ \  \
#  \ \  \ \\ \ \  \  __\ \  \ \  \\|__| \  \
#   \ \  \_\\ \ \  \|\__\_\  \ \  \    \ \  \
#    \ \_______\ \____________\ \__\    \ \__\
#     \|_______|\|____________|\|__|     \|__|

#
# DWM Installation Script
# by Oleksii Kapula
#

set -l deps \
  "base-devel" "git" \
  "libx11" "libxft" "libxinerama" \
  "feh" "brightnessctl" "sxhkd" "xbanish" \
  "pipewire" "pipewire-alsa" "pipewire-pulse" "pipewire-jack" \
  "wireplumber" "alsa-utils"

set -l xdeps "server" "xinit" "xprop" "xsetroot" "xset" "xinput"

for dep in $xdeps
  set -a deps (string join "" "xorg-" $dep)
end

if not type -q yay
  sudo pacman --sync --needed base-devel git
  git clone https://aur.archlinux.org/yay-bin.git
  cd yay-bin/ && makepkg -si
  cd - && rm yay-bin/ -rf
end

yay --sync --needed $deps

set -l sldir $HOME/.dotfiles/suckless

if not test -d $sldir
  mkdir -p $sldir
end

set -l tools "dwm" "dmenu"

for tool in $tools
  if test -d $sldir/$tool; continue; end
  git clone https://git.suckless.org/$tool $sldir/$tool
  cd $sldir/$tool
  make && sudo make clean install
  cd -
end

if not test -d /usr/bin/xsessions
  sudo mkdir /usr/bin/xsessions
end

printf "%s\n%s\n%s\n%s\n%s" \
  "[Desktop Entry]" \
  "Type=XSession" \
  "Exec=$(which dwm)" \
  "Comment=Dynamic Window Manager" \
  "Name=DWM" > $HOME/dwm.desktop

if test -e /usr/share/xsessions/dwm.desktop
  rm $HOME/dwm.desktop
else
  sudo mv $HOME/dwm.desktop /usr/share/xsessions
end
